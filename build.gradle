plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.8'
    id 'idea'
}

group 'au.com.nab.daf'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.9.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.27.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.25.0'
    implementation 'io.grpc:grpc-protobuf:1.27.1'
    implementation 'io.grpc:grpc-stub:1.27.1'
    implementation "io.grpc:grpc-services:1.15.1"   // reflection

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// if you have source imports issues, add the below
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')
idea {
    module {
        // Marks the already(!) added srcDir as "generated"
        generatedSourceDirs += file('build/generated/source')
    }
}

task runServer(type: JavaExec) {
    group = "Execution"
    description = "Start gRPC server"
    classpath = sourceSets.main.runtimeClasspath
    main = "au.com.nab.daf.grpc.greeting.server.GreetingServer"
}

task runClient(type: JavaExec) {
    group = "Execution"
    description = "Start gRPC client"
    classpath = sourceSets.main.runtimeClasspath
    main = "au.com.nab.daf.grpc.greeting.client.GreetingClient"
}